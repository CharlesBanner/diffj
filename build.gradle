apply plugin: 'java'
apply plugin: 'application'

group = 'org.incava.diffj'
version ='1.4.0'
mainClassName = 'org.incava.diffj.DiffJ'

repositories {
    mavenCentral()
}

dependencies {
    compile files('libs/pmd-4.2.5.jar')
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

allprojects {
    tasks.withType(Compile).all { Compile compile ->
        compile.options.debug = true
        compile.options.compilerArgs = ['-Xlint:all']
    }
}

jar { 
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    manifest { attributes 'Main-Class' : 'org.incava.diffj.DiffJ' }
}

test {
    // excludes = [ '**/IncavaTestCase*', '**/.#*' ]
    includes = [ '**/Test*' ]
}

logging.captureStandardOutput LogLevel.ERROR

// -- PMD configuration --

configurations {
    pmdConf
}
 
dependencies {
    pmdConf 'pmd:pmd:4.2.5'
}
 
task pmd << {
    println 'Running PMD static code analysis'
    ant {
        taskdef(name:'pmd', classname:'net.sourceforge.pmd.ant.PMDTask', classpath: configurations.pmdConf.asPath)
 
        pmd(shortFilenames:'true', failonruleviolation:'true', rulesetfiles:'rulesets/basic.xml') {
            formatter(type:'text', toConsole:'true')
            fileset(dir: "src/main/java") {
                include(name: '**/*.java')
            }
            fileset(dir: "src/test/java") {
                include(name: '**/*.java')
            }
        }
    }
}
