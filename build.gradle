apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'pmd'

group = 'org.incava.diffj'
version ='1.5.1'
mainClassName = 'org.incava.diffj.app.DiffJ'

repositories {
    mavenCentral()
}

dependencies {
    compile files('libs/pmdx-3.0.0.jar')
    compile files('libs/ijdk-3.4.2.jar')
    compile files('libs/jagol-1.1.0.jar')
    compile files('libs/java-diff-1.0.0.jar')
    compile files('libs/qualog-2.2.0.jar')
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

allprojects {
    tasks.withType(JavaCompile).all { JavaCompile compile ->
        compile.options.debug = true
        compile.options.compilerArgs = ['-Xlint:all']
    }
}

jar { 
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
    manifest { attributes 'Main-Class' : 'org.incava.diffj.app.DiffJ' }
}

test {
    excludes = [ '**/IncavaTestCase*', '**/.#*', '**/DiffJTest.class', '**/ItemsTest.class', '**/OutputTest.class' ]
    includes = [ '**/Test*' ]
    testLogging {
        exceptionFormat = "full"
    }
}

logging.captureStandardOutput LogLevel.ERROR

// -- PMD configuration --

configurations {
    pmdConf
}
 
dependencies {
    pmdConf 'pmd:pmd:4.2.5'
}
